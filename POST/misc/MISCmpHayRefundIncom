/****o* Mercado Pago/OFB:post:MISCmpHayRefundIncom
* NOMBRE
*   OFB:post:MISCmpHayRefundIncom
* DESCRIPCION
*   Verifica si existen Refunds Smart Point incompletos (DEV) pendientes de journalizacion
* SOURCE
*/
#include "src\POST\misc\postmisc.h"

SUB post:MISCmpHayRefundIncom
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_more LIKE lib:err_retval

	DPRINT "[DBG] MISCmpHayRefundIncom: filtros fecha=[%s] login=[%s] termi=[%s] operacion=[DEV] comando=[POINT_REFUND_POST] codint!=[QRI] jnlOK=[N]\n" common:system_date op:op_login_id common:window_node

	CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
		WHERE post:adt_opmp_fecha == common:system_date \
		&& post:adt_opmp_login_id == op:op_login_id \
		&& post:adt_opmp_termi_win == common:window_node \
		&& post:adt_opmp_operacion == "DEV" \
		&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
		&& post:adt_opmp_codint_mp != "QRI" \
		&& post:adt_opmp_jnlOK == "N"

	CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:OperacionesMP_ctx \
			post:adt_opmp_operacion \
			post:adt_opmp_comandos \
			post:adt_opmp_codint_mp \
			post:adt_opmp_jnlOK \
			post:adt_opmp_codseq_nro \
			post:adt_opmp_PresNro \
			post:adt_opmp_order_id \
			post:adt_opmp_order_id_PS \
			post:adt_opmp_ref_id \
			post:adt_opmp_ref_id_PS \
			post:adt_opmp_ref_st_PS \
			post:adt_opmp_st_det_PS \
			post:adt_opmp_status_p_PS \
			post:adt_opmp_payment_id
		DPRINT "[DBG] MISCmpHayRefundIncom: encontrado codseq=[%s] pres=[%s] op=[%s] cmd=[%s] codint=[%s] jnlOK=[%s]\n" post:adt_opmp_codseq_nro post:adt_opmp_PresNro post:adt_opmp_operacion post:adt_opmp_comandos post:adt_opmp_codint_mp post:adt_opmp_jnlOK
		DPRINT "[DBG] MISCmpHayRefundIncom: cols order=[%s] orderPS=[%s] ref=[%s] refPS=[%s]\n" post:adt_opmp_order_id post:adt_opmp_order_id_PS post:adt_opmp_ref_id post:adt_opmp_ref_id_PS
		DPRINT "[DBG] MISCmpHayRefundIncom: cols refstPS=[%s] stdetPS=[%s] stpayPS=[%s] payid=[%s]\n" post:adt_opmp_ref_st_PS post:adt_opmp_st_det_PS post:adt_opmp_status_p_PS post:adt_opmp_payment_id

		CALL csr:proc_list(F_COPY,LST:post:LISTget_opmp_order,LST:post:LISTset_opmp_order)
		CALL csr:proc_list(F_COPY,LST:post:LISTget_opmp_refund,LST:post:LISTset_opmp_refund)
		DPRINT "[DBG] MISCmpHayRefundIncom: post orderPS=[%s] payPS=[%s] refPS=[%s] refstPS=[%s]\n" post:mp_order_id_PS post:mp_payment_id_PS post:mp_refund_id_PS post:mp_refund_status_PS
		DPRINT "[DBG] MISCmpHayRefundIncom: post ref=[%s] refst=[%s] stdet=[%s]\n" post:mp_refund_id post:mp_refund_status post:mp_status_detail_PS
		CDS FINDNEXT DSC:post:OperacionesMP_ctx NOLOCK
		LET l_more = CDSRETVAL
		IF (l_more == 0) THEN
			DPRINT "[DBG] WARNING MISCmpHayRefundIncom: se detecto mas de 1 refund incompleto. Se procesara solo el primero.\n"
		END IF
	ELSE
		DPRINT "[DBG] MISCmpHayRefundIncom: no se encontraron refunds incompletos Smart Point.\n"
	END IF
	CDS ENDSELECT DSC:post:OperacionesMP_ctx
	DPRINT "[DBG] MISCmpHayRefundIncom: retorno RETVAL=[%d]\n" l_retval

	RETURN l_retval

END SUB

/*******/
