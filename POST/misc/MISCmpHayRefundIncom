/****o* Mercado Pago/OFB:post:MISCmpHayRefundIncom
* NOMBRE
*   OFB:post:MISCmpHayRefundIncom
* DESCRIPCION
*   Verifica si existe un Refund Smart Point incompleto (DEV)
* SOURCE
*/
#include "src\POST\misc\postmisc.h"

SUB post:MISCmpHayRefundIncom(p_codseq,p_presnro)
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_jnlret LIKE lib:err_retval
	LOCAL l_found LIKE lib:err_retval
	LOCAL l_codsiaf LIKE siaf:codigo_siaf
	LOCAL l_codseq LIKE post:adt_opmp_codseq_nro
	LOCAL l_pres LIKE post:adt_opmp_PresNro
	LOCAL l_jnlok LIKE post:adt_opmp_jnlOK

	LET l_found = -2
	CLEARFIELD p_codseq
	CLEARFIELD p_presnro
	CLEARFIELD l_codseq
	CLEARFIELD l_pres
	CLEARFIELD l_jnlok

	DPRINT "[DBG] HayRefInc: ini fec=[%s] ter=[%s] log=[%s]\n" common:system_date common:window_node op:op_login_id

	CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
		WHERE post:adt_opmp_fecha == common:system_date \
		&& post:adt_opmp_termi_win == common:window_node \
		&& post:adt_opmp_login_id == op:op_login_id \
		&& post:adt_opmp_operacion == "DEV" \
		&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
		&& post:adt_opmp_codint_mp != "QRI" \
		&& post:adt_opmp_status_p_PS != "canceled"

	CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:OperacionesMP_ctx \
			post:adt_opmp_codseq_nro INTO l_codseq \
			post:adt_opmp_PresNro INTO l_pres \
			post:adt_opmp_jnlOK INTO l_jnlok \
			post:adt_opmp_cod_siaf

		IF (post:adt_opmp_cod_siaf == 99601) THEN
			LET l_codsiaf = 99601
		ELSE
			LET l_codsiaf = 99600
		END IF

		CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
			tlr:jnl_bus_date == common:system_date && \
			siaf:jnl_window_node == common:window_node && \
			siaf:codigo_siaf == l_codsiaf && \
			post:PresNro == l_pres && \
			siaf:moneda == "06" && \
			tlr:jnl_op_login_id == op:op_login_id && \
			post:jnl_opmp_codseq_nro == l_codseq

		CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
		LET l_jnlret = CDSRETVAL
		CDS ENDSELECT DSC:tlr:jnl_context

		DPRINT "[DBG] HayRefInc: op cod=[%s] pre=[%s] jok=[%s] jnl=[%d]\n" l_codseq l_pres l_jnlok l_jnlret

		IF (l_jnlok != "S" || l_jnlret != 0) THEN
			LET l_found = 0
			LET p_codseq = l_codseq
			LET p_presnro = l_pres
			LET post:adt_opmp_codseq_nro = l_codseq
			LET post:adt_opmp_PresNro = l_pres
		END IF
	END IF
	CDS ENDSELECT DSC:post:OperacionesMP_ctx

	IF (l_found != 0) THEN
		CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
			tlr:jnl_bus_date == common:system_date && \
			siaf:jnl_window_node == common:window_node && \
			tlr:jnl_op_login_id == op:op_login_id && \
			siaf:moneda == "06" && \
			siaf:codigo_siaf == 99600

		CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
		LET l_jnlret = CDSRETVAL
		IF (l_jnlret != 0) THEN
			CDS ENDSELECT DSC:tlr:jnl_context
			CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
				tlr:jnl_bus_date == common:system_date && \
				siaf:jnl_window_node == common:window_node && \
				tlr:jnl_op_login_id == op:op_login_id && \
				siaf:moneda == "06" && \
				siaf:codigo_siaf == 99601
			CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
			LET l_jnlret = CDSRETVAL
		END IF

		IF (l_jnlret == 0) THEN
			CDS EXTRACT DSC:tlr:jnl_context \
				post:jnl_opmp_codseq_nro INTO l_codseq \
				post:PresNro INTO l_pres
			DPRINT "[DBG] HayRefInc: jnl cod=[%s] pre=[%s]\n" l_codseq l_pres

			CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
				WHERE post:adt_opmp_fecha == common:system_date \
				&& post:adt_opmp_codseq_nro == l_codseq \
				&& post:adt_opmp_PresNro == l_pres \
				&& post:adt_opmp_operacion == "DEV" \
				&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
				&& post:adt_opmp_codint_mp != "QRI" \
				&& post:adt_opmp_status_p_PS != "canceled"

			CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
			IF (CDSRETVAL == 0) THEN
				CDS EXTRACT DSC:post:OperacionesMP_ctx post:adt_opmp_jnlOK INTO l_jnlok
				IF (l_jnlok != "S") THEN
					LET l_found = 0
					LET p_codseq = l_codseq
					LET p_presnro = l_pres
					LET post:adt_opmp_codseq_nro = l_codseq
					LET post:adt_opmp_PresNro = l_pres
				END IF
			END IF
			CDS ENDSELECT DSC:post:OperacionesMP_ctx
		END IF
		CDS ENDSELECT DSC:tlr:jnl_context
	END IF

	IF (l_found != 0) THEN
		DPRINT "[DBG] HayRefInc: sin refund incompleto\n"
	END IF

	DPRINT "[DBG] HayRefInc: ret=[%d] cod=[%s] pre=[%s]\n" l_found p_codseq p_presnro
	RETURN l_found

END SUB

/*******/
