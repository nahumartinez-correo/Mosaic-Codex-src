/****o* Mercado Pago/OFB:post:MISCmpHayRefundIncom
* NOMBRE
*   OFB:post:MISCmpHayRefundIncom
* DESCRIPCION
*   Verifica si existe un Refund Smart Point incompleto (DEV)
* SOURCE
*/
#include "src\POST\misc\postmisc.h"

SUB post:MISCmpHayRefundIncom
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_jnlret LIKE lib:err_retval
	LOCAL l_found LIKE lib:err_retval
	LOCAL l_codsiaf LIKE siaf:codigo_siaf

	LET l_found = -2

	DPRINT "[DBG] HayRefInc: ini fec=[%s] log=[%s] ter=[%s] op=[DEV] cmd=[POINT_REFUND_POST]\n" common:system_date op:op_login_id common:window_node

	CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
		WHERE post:adt_opmp_fecha == common:system_date \
		&& post:adt_opmp_termi_win == common:window_node \
		&& post:adt_opmp_login_id == op:op_login_id \
		&& post:adt_opmp_operacion == "DEV" \
		&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
		&& post:adt_opmp_codint_mp != "QRI" \
		&& post:adt_opmp_status_p_PS != "canceled" \
		&& post:adt_opmp_jnlOK == "N"

	CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:OperacionesMP_ctx \
			post:adt_opmp_codseq_nro \
			post:adt_opmp_PresNro \
			post:adt_opmp_jnlOK \
			post:adt_opmp_codint_mp \
			post:adt_opmp_cod_siaf \
			post:adt_opmp_order_id_PS \
			post:adt_opmp_ref_id_PS \
			post:adt_opmp_ref_st_PS \
			post:adt_opmp_amount
		LET l_found = 0
		DPRINT "[DBG] HayRefInc: por jnl cod=[%s] pre=[%s] jnl=[%s] codint=[%s]\n" post:adt_opmp_codseq_nro post:adt_opmp_PresNro post:adt_opmp_jnlOK post:adt_opmp_codint_mp
		DPRINT "[DBG] HayRefInc: ids ord=[%s] ref=[%s] st=[%s] imp=[%s]\n" post:adt_opmp_order_id_PS post:adt_opmp_ref_id_PS post:adt_opmp_ref_st_PS post:adt_opmp_amount
	END IF
	CDS ENDSELECT DSC:post:OperacionesMP_ctx

	IF (l_found != 0) THEN
		CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
			WHERE post:adt_opmp_fecha == common:system_date \
			&& post:adt_opmp_termi_win == common:window_node \
			&& post:adt_opmp_login_id == op:op_login_id \
			&& post:adt_opmp_operacion == "DEV" \
			&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
			&& post:adt_opmp_codint_mp != "QRI" \
			&& post:adt_opmp_status_p_PS != "canceled"

		CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
		LET l_retval = CDSRETVAL
		IF (l_retval == 0) THEN
			CDS EXTRACT DSC:post:OperacionesMP_ctx \
				post:adt_opmp_codseq_nro \
				post:adt_opmp_PresNro \
				post:adt_opmp_jnlOK \
				post:adt_opmp_cod_siaf \
				post:adt_opmp_order_id_PS \
				post:adt_opmp_ref_id_PS \
				post:adt_opmp_ref_st_PS \
				post:adt_opmp_amount

			IF (post:adt_opmp_cod_siaf == 99601) THEN
				LET l_codsiaf = 99601
			ELSE
				LET l_codsiaf = 99600
			END IF

			CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
				tlr:jnl_bus_date == common:system_date && \
				siaf:jnl_window_node == common:window_node && \
				siaf:codigo_siaf == l_codsiaf && \
				post:PresNro == post:adt_opmp_PresNro && \
				siaf:moneda == "06" && \
				tlr:jnl_op_login_id == op:op_login_id && \
				post:jnl_opmp_codseq_nro == post:adt_opmp_codseq_nro

			CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
			LET l_jnlret = CDSRETVAL
			CDS ENDSELECT DSC:tlr:jnl_context

			IF (l_jnlret != 0) THEN
				LET l_found = 0
				DPRINT "[DBG] HayRefInc: falta jnl cod=[%s] pre=[%s] cods=[%s] jnl=[%s]\n" post:adt_opmp_codseq_nro post:adt_opmp_PresNro l_codsiaf post:adt_opmp_jnlOK
				DPRINT "[DBG] HayRefInc: ids ord=[%s] ref=[%s] st=[%s] imp=[%s]\n" post:adt_opmp_order_id_PS post:adt_opmp_ref_id_PS post:adt_opmp_ref_st_PS post:adt_opmp_amount
			END IF
		END IF
		CDS ENDSELECT DSC:post:OperacionesMP_ctx
	END IF

	IF (l_found != 0) THEN
		CLEARFIELD post:adt_opmp_codseq_nro
		CLEARFIELD post:adt_opmp_PresNro
		DPRINT "[DBG] HayRefInc: sin refund incompleto\n"
	END IF

	DPRINT "[DBG] HayRefInc: ret=[%d]\n" l_found
	RETURN l_found

END SUB

/*******/
