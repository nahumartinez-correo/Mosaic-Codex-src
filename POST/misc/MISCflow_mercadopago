/****o* Mercado Pago/OFB:post:MISCflow_mercadopago
* NOMBRE
*   OFB:post:MISCflow_mercadopago
* DESCRIPCION
*   Tratamiento de operaciones con tarjeta Mercado Pago
* ENTRADAS
*   Par�metros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deber�n ser analizados al modificar el objeto actual
* REFERENCIADO POR
*  post:MISCsplit_flow_card
* DOCUMENTACION
*   NI50546 Mercado Pago 
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCflow_mercadopago
	SETFLAG drv:mode_flags,popup_mode

	IF (post:adt_tmp_codint_mp == "MPC" || post:adt_tmp_codint_mp == "MPD") THEN
		//Es POINT Integrado
		POPUP SCR:post:SCRNmpPurchase,post:PRESmpPurchase,post:PRECmpPurchase,post:SVALmpPurchase
		RESETFLAG drv:mode_flags,popup_mode
		IF (LASTKEY == RT_ESC || LASTKEY == RT_PAGEUP || LASTKEY == RT_BAILOUT) THEN
			IF (post:mp_order_id_PS != "") THEN
				//Verfico el estado de la Orden
				CALL post:MISCmpGetOrderPoint
				IF (RETVAL < 0) THEN
					//Dio ESC sin haber generado orden de pago
					RETURN -2
				ELSE
					IF (post:mp_status_pago_PS == "processed") THEN
						MSGBOX "El pago ya fue realizado, si desea anularlo deber� hacerlo por Anulacion de Comprobantes",0x00,"ATENCION"
						//Fuerzo la confirmacion porque ya se acredito el pago!!!
						LASTKEY = RT_SEND
					ELSE
						//Si existe la Orden y no se efectuo el pago entonces cancelo la Orden  
						CALL post:MISCmpDelOrderPoint
						IF (post:mp_response_PS == 409) THEN
							WHILE (post:mp_status_PS != "canceled" && post:mp_status_PS != "processed" && post:mp_status_PS != "failed") THEN
								CALL post:MISCmpGetOrderPoint
								IF (post:mp_status_PS != "canceled" && post:mp_status_PS != "failed") THEN
									MSGBOX "Debe cancelar la orden de pago generada desde la terminal Point",0x00,"ATENCION"
								END IF
							END WHILE
						END IF 
						IF (post:mp_status_PS == "canceled") THEN
							//Actualizo la marca en la tabla OperacionesMP
							CALL post:CDSopmp_jnlFallo("VEN")
							MSGBOX "Ha cancelado la orden de pago generada",0x00,"ATENCION"
							RETURN -2
						ELSEIF (post:mp_status_PS == "processed") THEN
							MSGBOX "El pago ya fue realizado, si desea anularlo deber� hacerlo por Anulacion de Comprobantes",0x00,"ATENCION"
							//Fuerzo la confirmacion porque ya se acredito el pago!!!
							LASTKEY = RT_SEND
						ELSEIF (post:mp_status_PS == "failed") THEN
							//Actualizo la marca en la tabla OperacionesMP
							CALL post:CDSopmp_jnlFallo("VEN")
							MSGBOX "La orden presento un error. Se cancela la orden",0x00,"ATENCION"
							RETURN -2
						END IF
					END IF
				END IF
			ELSE
				//No se genero Orden
				RETURN -2
			END IF
		END IF
	ELSE
		//Es QR Integrado
		POPUP SCR:post:SCRNmpPurchaseQR,post:PRESmpPurchase,post:PRECmpPurchase,post:SVALmpPurchaseQR
		RESETFLAG drv:mode_flags,popup_mode
		IF (LASTKEY == RT_ESC || LASTKEY == RT_PAGEUP || LASTKEY == RT_BAILOUT) THEN
			IF (post:mp_status.NUMCHARS > 0) THEN
			  //Hago un Search para traer el detalle del Pago
				CALL post:MISCmpSearchPayment
				IF (RETVAL < 0) THEN
					//Cancelo la ultima Orden 
					IF (post:adt_opmp_status == "OPEN") THEN
						CALL post:MISCmpDelOrderQR
						
						//Actualizo la marca en la tabla OperacionesMP
						CALL post:CDSopmp_jnlFallo("VEN")
					
						MSGBOX "Ha cancelado la orden de pago generada",0x00,"ATENCION"
					END IF
					RETURN -2
				ELSE
					IF (post:mp_status_pago == "approved") THEN
						MSGBOX "El pago ya fue realizado, si desea anularlo deber� hacerlo por Anulacion de Comprobantes",0x00,"ATENCION"
						//Fuerzo la confirmacion porque ya se acredito el pago!!!
						LASTKEY = RT_SEND
					ELSE
						RETURN -2
					END IF
				END IF
			ELSE
				//No se genero Orden
				RETURN -2
			END IF
		END IF
	END IF

	//Limpio el buffer del teclado
	INIT 33,0

  IF (post:adt_tmp_codint_mp == "MPC" || post:adt_tmp_codint_mp == "MPD") THEN
    //Si no hubo error y no salio con ESC
    LET post:NroCompPago2 = post:mp_reference_id
    LET post:Nro_Lote_Pos = post:mp_order_id_PS
    LET post:NroTransac = post:mp_reference_id
    LET post:Tipo_Operacion_Pos = "ONLINE"
  ELSE
    //Imprimo Ticko
    CALL MISCticko_MP

    LET post:NroCompPago2 = post:mp_payment_id 
    LET post:Nro_Lote_Pos = post:mp_order_id     
    LET post:NroTransac = post:mp_auth_code
    LET post:Tipo_Operacion_Pos = "ONLINE"
  END IF
	
	REFRESH post:NroCompPago2
	REFRESH post:Nro_Lote_Pos
	REFRESH post:NroTransac
	REFRESH post:Tipo_Operacion_Pos
	
END SUB

/*******/
