/****o* Mercado Pago/OFB:post:MISCmpRefRecov
* NOMBRE
*   OFB:post:MISCmpRefRecov
* DESCRIPCION
*   Recupera refund Smart Point incompleto al iniciar login
* SOURCE
*/
#include "src\POST\misc\postmisc.h"

SUB post:MISCmpRefRecov
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_rec_ret LIKE lib:err_retval
	LOCAL l_codseq LIKE post:adt_opmp_codseq_nro
	LOCAL l_pres LIKE post:adt_opmp_PresNro
	LOCAL l_jnlok LIKE post:adt_opmp_jnlOK
	LOCAL l_codint LIKE post:adt_opmp_codint_mp
	LOCAL l_cmd LIKE post:adt_opmp_comandos
	LOCAL l_jnl_codseq LIKE post:jnl_opmp_codseq_nro
	LOCAL l_jnl_pres LIKE post:PresNro
	LOCAL l_jnlret LIKE lib:err_retval
	LOCAL l_has_pending LIKE lib:err_retval
	LOCAL l_jnl_siaf LIKE siaf:codigo_siaf
	LOCAL l_jnl_mon LIKE siaf:moneda
	LOCAL l_jnl_log LIKE op:op_login_id
	LOCAL l_jnl_term LIKE common:window_node
	LOCAL l_loc_jnl_ret LIKE lib:err_retval
	LOCAL l_Total_importe LIKE post:Total_importe
	LOCAL l_codigo_interno LIKE post:codigo_interno
	LOCAL l_NroCompPago2 LIKE post:NroCompPago2
	LOCAL l_Nro_Lote_Pos LIKE post:Nro_Lote_Pos
	LOCAL l_NroTransac LIKE post:NroTransac
	LOCAL l_Tipo_Operacion_Pos LIKE post:Tipo_Operacion_Pos
	LOCAL l_importe_origen LIKE post:importe_origen
	LOCAL l_moneda LIKE siaf:moneda
	LOCAL l_codigo_medio_pago LIKE post:codigo_medio_pago
	LOCAL l_DescripMedPag LIKE post:DescripMedPag
	LOCAL l_medio_pago_desc LIKE post:medio_pago_desc
	LOCAL l_codint_tarj_desc LIKE post:codint_tarj_desc
	LOCAL l_cod_siaf_dev LIKE siaf:codigo_siaf
	LOCAL l_upd_find LIKE lib:err_retval
	LOCAL l_upd_ret LIKE lib:err_retval
	LOCAL l_jnl_after LIKE post:adt_opmp_jnlOK

	LET l_has_pending = -2
	CLEARFIELD l_codseq
	CLEARFIELD l_pres
	CLEARFIELD l_jnlok
	CLEARFIELD l_codint
	CLEARFIELD l_cmd

	DPRINT "[DBG] RefRecov: ini fec=[%s] log=[%s] ter=[%s]\n" common:system_date op:op_login_id common:window_node
	DPRINT "[DBG] RefRecov: ini mp=[%s]\n" common:adt_mp_termi_integ

	CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
		WHERE post:adt_opmp_fecha == common:system_date \
		&& post:adt_opmp_login_id == op:op_login_id \
		&& post:adt_opmp_termi_win == common:window_node \
		&& post:adt_opmp_operacion == "DEV" \
		&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
		&& post:adt_opmp_codint_mp != "QRI" \
		&& post:adt_opmp_jnlOK == "N"

	CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:OperacionesMP_ctx \
			post:adt_opmp_operacion \
			post:adt_opmp_comandos \
			post:adt_opmp_codint_mp \
			post:adt_opmp_jnlOK \
			post:adt_opmp_codseq_nro \
			post:adt_opmp_PresNro \
			post:adt_opmp_order_id \
			post:adt_opmp_order_id_PS \
			post:adt_opmp_ref_id \
			post:adt_opmp_ref_id_PS \
			post:adt_opmp_ref_st_PS \
			post:adt_opmp_st_det_PS \
			post:adt_opmp_status_p_PS \
			post:adt_opmp_payment_id

		LET l_has_pending = 0
		LET l_codseq = post:adt_opmp_codseq_nro
		LET l_pres = post:adt_opmp_PresNro
		LET l_jnlok = post:adt_opmp_jnlOK
		LET l_codint = post:adt_opmp_codint_mp
		LET l_cmd = post:adt_opmp_comandos
		DPRINT "[DBG] RefRecov: opmp enc=[%d] cod=[%s] pre=[%s] jnl=[%s] cin=[%s]\n" l_has_pending l_codseq l_pres l_jnlok l_codint
		DPRINT "[DBG] RefRecov: opmp cmd=[%s] ord=[%s] ref=[%s] st=[%s]\n" l_cmd post:adt_opmp_order_id_PS post:adt_opmp_ref_id_PS post:adt_opmp_ref_st_PS
	ELSE
		DPRINT "[DBG] RefRecov: opmp enc=[%d]\n" l_retval
	END IF
	CDS ENDSELECT DSC:post:OperacionesMP_ctx

	IF (l_has_pending != 0) THEN
		CLEARFIELD l_jnl_codseq
		CLEARFIELD l_jnl_pres
		CLEARFIELD l_jnl_siaf
		CLEARFIELD l_jnl_mon
		CLEARFIELD l_jnl_log
		CLEARFIELD l_jnl_term
		LET l_jnlret = -2

		CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
			tlr:jnl_bus_date == common:system_date && \
			siaf:jnl_window_node == common:window_node && \
			tlr:jnl_op_login_id == op:op_login_id && \
			siaf:codigo_siaf == 99600 && \
			siaf:moneda == "06" && \
			post:PresNro == l_pres && \
			post:jnl_opmp_codseq_nro == l_codseq
		CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
		LET l_jnlret = CDSRETVAL
		IF (l_jnlret == 0) THEN
			CDS EXTRACT DSC:tlr:jnl_context \
				post:jnl_opmp_codseq_nro \
				post:PresNro \
				siaf:codigo_siaf \
				siaf:moneda \
				tlr:jnl_op_login_id \
				siaf:jnl_window_node
			LET l_jnl_codseq = post:jnl_opmp_codseq_nro
			LET l_jnl_pres = post:PresNro
			LET l_jnl_siaf = siaf:codigo_siaf
			LET l_jnl_mon = siaf:moneda
			LET l_jnl_log = tlr:jnl_op_login_id
			LET l_jnl_term = siaf:jnl_window_node
		END IF
		CDS ENDSELECT DSC:tlr:jnl_context
		DPRINT "[DBG] RefRecov: jnl99600 rv=[%d] cod=[%s] pre=[%s] siaf=[%s]\n" l_jnlret l_jnl_codseq l_jnl_pres l_jnl_siaf
		DPRINT "[DBG] RefRecov: jnl99600 mon=[%s] log=[%s] ter=[%s]\n" l_jnl_mon l_jnl_log l_jnl_term

		IF (l_jnlret != 0) THEN
			CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
				tlr:jnl_bus_date == common:system_date && \
				siaf:jnl_window_node == common:window_node && \
				tlr:jnl_op_login_id == op:op_login_id && \
				siaf:codigo_siaf == 99601 && \
				siaf:moneda == "06" && \
				post:PresNro == l_pres && \
				post:jnl_opmp_codseq_nro == l_codseq
			CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
			LET l_jnlret = CDSRETVAL
			IF (l_jnlret == 0) THEN
				CDS EXTRACT DSC:tlr:jnl_context \
					post:jnl_opmp_codseq_nro \
					post:PresNro \
					siaf:codigo_siaf \
					siaf:moneda \
					tlr:jnl_op_login_id \
					siaf:jnl_window_node
				LET l_jnl_codseq = post:jnl_opmp_codseq_nro
				LET l_jnl_pres = post:PresNro
				LET l_jnl_siaf = siaf:codigo_siaf
				LET l_jnl_mon = siaf:moneda
				LET l_jnl_log = tlr:jnl_op_login_id
				LET l_jnl_term = siaf:jnl_window_node
			END IF
			CDS ENDSELECT DSC:tlr:jnl_context
			DPRINT "[DBG] RefRecov: jnl99601 rv=[%d] cod=[%s] pre=[%s] siaf=[%s]\n" l_jnlret l_jnl_codseq l_jnl_pres l_jnl_siaf
			DPRINT "[DBG] RefRecov: jnl99601 mon=[%s] log=[%s] ter=[%s]\n" l_jnl_mon l_jnl_log l_jnl_term
		END IF
	END IF

	IF (l_has_pending == 0) THEN
		MSGBOX "SE DETECTO UN REEMBOLSO SIN COMPLETARSE POR UNA CAIDA O INTERRUPCION. SE PROCEDE A COMPLETARLO ANTES DE INICIAR EL PROGRAMA",OFB_OK,"AVISO"
		DPRINT "[DBG] RefRecov: pre-dev cod=[%s] pre=[%s] cin=[%s]\n" l_codseq l_pres l_codint
		DPRINT "[DBG] RefRecov: pre-dev cmd=[%s] jnl=[%s] rvjnl=[%d]\n" l_cmd l_jnlok l_jnlret

		IF (l_jnlret == 0) THEN
			LET post:PresNro = l_pres
			LET post:adt_vpi_codseq_nro = l_codseq
			LET post:adt_opmp_codint_mp = l_codint
			LET post:mp_comandos = l_cmd
			DPRINT "[DBG] RefRecov: seal ctx siaf=[%s] pre=[%s] cod=[%s]\n" l_jnl_siaf post:PresNro post:adt_vpi_codseq_nro
			CALL post:CDSopmp_jnlOK("REFUND")
			LET l_rec_ret = RETVAL
			DPRINT "[DBG] RefRecov: seal ret=[%d]\n" l_rec_ret
		ELSE
			LET l_loc_jnl_ret = -2
			CLEARFIELD l_cod_siaf_dev

			CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
				WHERE post:adt_opmp_fecha == common:system_date \
				&& post:adt_opmp_login_id == op:op_login_id \
				&& post:adt_opmp_termi_win == common:window_node \
				&& post:adt_opmp_codseq_nro == l_codseq
			CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
			IF (CDSRETVAL == 0) THEN
				CDS EXTRACT DSC:post:OperacionesMP_ctx \
					post:adt_opmp_cod_siaf \
					post:adt_opmp_ref_id \
					post:adt_opmp_order_id \
					post:adt_opmp_auth_code \
					post:adt_opmp_amount \
					post:adt_opmp_payment_id \
					post:adt_opmp_codint_mp
			END IF
			CDS ENDSELECT DSC:post:OperacionesMP_ctx

			CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
				tlr:jnl_bus_date == common:system_date && \
				siaf:jnl_window_node == common:window_node && \
				siaf:codigo_siaf == 99099 && \
				post:PresNro == l_pres && \
				siaf:moneda == "06" && \
				tlr:jnl_op_login_id == op:op_login_id && \
				post:jnl_opmp_codseq_nro == l_codseq
			CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
			LET l_loc_jnl_ret = CDSRETVAL
			IF (l_loc_jnl_ret == 0) THEN
				CDS EXTRACT DSC:tlr:jnl_context \
					post:Total_importe \
					post:codigo_interno \
					post:NroCompPago2 \
					post:Nro_Lote_Pos \
					post:NroTransac \
					post:Tipo_Operacion_Pos \
					post:importe_origen \
					siaf:moneda \
					post:codigo_medio_pago \
					post:DescripMedPag \
					post:medio_pago_desc \
					post:codint_tarj_desc
			END IF
			CDS ENDSELECT DSC:tlr:jnl_context
			DPRINT "[DBG] RefRecov: jnl99099 rv=[%d] cod=[%s] pre=[%s] siaf=[%s]\n" l_loc_jnl_ret l_codseq l_pres l_cod_siaf_dev

			IF (l_loc_jnl_ret != 0) THEN
				LET post:Total_importe = post:adt_opmp_amount / 100
				LET post:codigo_interno = 130410
				LET post:NroCompPago2 = post:adt_opmp_payment_id
				LET post:Nro_Lote_Pos = post:adt_opmp_order_id
				LET post:NroTransac = post:adt_opmp_auth_code
				LET post:Tipo_Operacion_Pos = "ONLINE"
				LET post:importe_origen = post:adt_opmp_amount / 100
				LET siaf:moneda = "06"
				LET post:codigo_medio_pago = "06"

				CDS SELECT FROM DSC:post:MEDIOS_TAB BECOMES "MedioPago_ctx" \
					WHERE post:adt_mp_cod == siaf:moneda
				CDS FINDFIRST "MedioPago_ctx" NOLOCK
				IF (CDSRETVAL == 0) THEN
					CDS EXTRACT "MedioPago_ctx" post:adt_mp_desc INTO post:medio_pago_desc
				END IF
				CDS ENDSELECT "MedioPago_ctx"
				LET post:DescripMedPag = post:medio_pago_desc

				CDS SELECT FROM DSC:post:tarj_pos_tbl BECOMES DSC:post:tarj_pos_ctx \
					WHERE post:adt_tpos_codint_sap == post:codigo_interno \
					&& post:adt_tpos_codint_vpi == post:adt_opmp_codint_mp \
					&& post:adt_tpos_tipo_ope == "CO"
				CDS FINDFIRST DSC:post:tarj_pos_ctx NOLOCK
				IF (CDSRETVAL == 0) THEN
					CDS EXTRACT DSC:post:tarj_pos_ctx post:adt_tpos_tarj_desc INTO post:codint_tarj_desc
				END IF
				CDS ENDSELECT DSC:post:tarj_pos_ctx
			END IF

			LET l_Total_importe = post:Total_importe
			LET l_codigo_interno = post:codigo_interno
			LET l_codint_tarj_desc = post:codint_tarj_desc
			LET l_NroCompPago2 = post:NroCompPago2
			LET l_Nro_Lote_Pos = post:Nro_Lote_Pos
			LET l_NroTransac = post:NroTransac
			LET l_Tipo_Operacion_Pos = post:Tipo_Operacion_Pos
			LET l_importe_origen = post:importe_origen
			LET l_moneda = siaf:moneda
			LET l_codigo_medio_pago = post:codigo_medio_pago
			LET l_DescripMedPag = post:DescripMedPag
			LET l_medio_pago_desc = post:medio_pago_desc

			CALL tlr:JNLinitialize
			IF (post:adt_opmp_cod_siaf == 99601) THEN
				LET siaf:codigo_siaf = 99601
			ELSE
				LET siaf:codigo_siaf = 99600
			END IF
			LET l_cod_siaf_dev = siaf:codigo_siaf

			CALL siaf:PRESfecha_hora
			IF (RETVAL == 0) THEN
				LET post:NroCompPago2AN = post:adt_opmp_ref_id
				LET post:Nro_Lote_PosAN = post:adt_opmp_order_id
				LET post:NroTransacAN = post:adt_opmp_auth_code
				LET post:Tipo_Operacion_PosAN = "ONLINE"

				LET post:Total_importe = l_Total_importe
				LET post:codigo_interno = l_codigo_interno
				LET post:codint_tarj_desc = l_codint_tarj_desc
				LET post:NroCompPago2 = l_NroCompPago2
				LET post:Nro_Lote_Pos = l_Nro_Lote_Pos
				LET post:NroTransac = l_NroTransac
				LET post:Tipo_Operacion_Pos = l_Tipo_Operacion_Pos
				LET post:importe_origen = l_importe_origen
				LET siaf:moneda = l_moneda
				LET post:codigo_medio_pago = l_codigo_medio_pago
				LET post:DescripMedPag = l_DescripMedPag
				LET post:medio_pago_desc = l_medio_pago_desc
				LET giros:PresNro_canc = l_pres
				LET siaf:importe = post:Total_importe
				LET post:adt_opmp_codseq_nro = l_codseq
				LET post:adt_mp_codseq_nro = l_codseq
				LET post:adt_vpi_codseq_nro = l_codseq
				LET post:adt_opmp_codint_mp = l_codint
				LET post:adt_tmp_codint_mp = l_codint
				LET post:mp_comandos = l_cmd
				LET post:mp_operacion = "DEV"

				tlr:INDdcol_scrn[0] = SCR:post:SCRN99600
				tlr:INDtran_name = LST:post:TRAN99600
				tlr:INDjnl_append = OFB:post:JNL99600

				CALL post:MISCmuevo_campos_jou
				CALL siaf:MISClleno_modalidad
				LET post:PresNro = l_pres
				DPRINT "[DBG] RefRecov: jnl loc pre siaf=[%s] pre=[%s] cod=[%s]\n" l_cod_siaf_dev post:PresNro post:adt_vpi_codseq_nro

				CLEARFIELD post:PresNro
				UNGETKEY RT_SEND
				CALL tlr:JNLtran
				LET l_rec_ret = RETVAL
				UNGETKEY RT_SEND
				RESETFLAG drv:mode_flags,no_stack_list
				DPRINT "[DBG] RefRecov: jnl loc ret=[%d] siaf=[%s] cod=[%s]\n" l_rec_ret l_cod_siaf_dev l_codseq

				LET post:PresNro = l_pres
				LET post:adt_vpi_codseq_nro = l_codseq
				LET post:adt_opmp_codint_mp = l_codint
				CALL post:CDSopmp_jnlOK("REFUND")
				DPRINT "[DBG] RefRecov: seal2 ret=[%d] pre=[%s] cod=[%s]\n" RETVAL post:PresNro post:adt_vpi_codseq_nro
			ELSE
				LET l_rec_ret = -2
			END IF
		END IF

		LET l_upd_find = -2
		LET l_upd_ret = -2
		CLEARFIELD l_jnlok
		CLEARFIELD l_jnl_after

		IF (l_rec_ret == 0) THEN
			DPRINT "[DBG] RefRecov: upd opmp pre=[%s] cod=[%s]\n" l_pres l_codseq

			CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
				WHERE post:adt_opmp_fecha == common:system_date \
				&& post:adt_opmp_login_id == op:op_login_id \
				&& post:adt_opmp_termi_win == common:window_node \
				&& post:adt_opmp_operacion == "DEV" \
				&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
				&& post:adt_opmp_codseq_nro == l_codseq \
				&& post:adt_opmp_PresNro == l_pres \
				&& post:adt_opmp_codint_mp != "QRI"
			CDS FINDFIRST DSC:post:OperacionesMP_ctx LOCK
			LET l_upd_find = CDSRETVAL
			IF (l_upd_find == 0) THEN
				CDS EXTRACT DSC:post:OperacionesMP_ctx ALL
				LET l_jnlok = post:adt_opmp_jnlOK
				CDS APPEND DSC:post:OperacionesMP_ctx "S" AS post:adt_opmp_jnlOK
				CDS UPDATE DSC:post:OperacionesMP_ctx LOCK
				LET l_upd_ret = CDSRETVAL
			END IF
			CDS UNLOCK DSC:post:OperacionesMP_ctx
			CDS ENDSELECT DSC:post:OperacionesMP_ctx
			DPRINT "[DBG] RefRecov: upd opmp rv_find=[%d] jnl_before=[%s]\n" l_upd_find l_jnlok

			CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
				WHERE post:adt_opmp_fecha == common:system_date \
				&& post:adt_opmp_login_id == op:op_login_id \
				&& post:adt_opmp_termi_win == common:window_node \
				&& post:adt_opmp_operacion == "DEV" \
				&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
				&& post:adt_opmp_codseq_nro == l_codseq \
				&& post:adt_opmp_PresNro == l_pres
			CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
			IF (CDSRETVAL == 0) THEN
				CDS EXTRACT DSC:post:OperacionesMP_ctx post:adt_opmp_jnlOK INTO l_jnl_after
			END IF
			CDS ENDSELECT DSC:post:OperacionesMP_ctx
			DPRINT "[DBG] RefRecov: upd opmp rv_upd=[%d] jnl_after=[%s]\n" l_upd_ret l_jnl_after
		END IF

		IF (l_rec_ret == 0 && l_jnl_after == "S") THEN
			MSGBOX "SE COMPLETO EL REEMBOLSO INCONCLUSO",OFB_OK,"AVISO"
			RETURN 0
		END IF

		MSGBOX "NO SE PUDO COMPLETAR EL REEMBOLSO INCONCLUSO. CONTINUE CON EL LOGIN.",OFB_OK,"AVISO"
		RETURN -2
	END IF

	RETURN -2
END SUB

/******/
