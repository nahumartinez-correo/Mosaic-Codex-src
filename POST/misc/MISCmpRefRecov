/****o* Mercado Pago/OFB:post:MISCmpRefRecov
* NOMBRE
*   OFB:post:MISCmpRefRecov
* DESCRIPCION
*   Recupera refund Smart Point incompleto al iniciar login
* SOURCE
*/
#include "postmisc.h"

SUB post:MISCmpRefRecov
	LOCAL l_retval LIKE lib:err_retval
	LOCAL l_rec_ret LIKE lib:err_retval
	LOCAL l_codseq LIKE post:adt_opmp_codseq_nro
	LOCAL l_pres LIKE post:adt_opmp_PresNro
	LOCAL l_jnlok LIKE post:adt_opmp_jnlOK
	LOCAL l_codint LIKE post:adt_opmp_codint_mp
	LOCAL l_cmd LIKE post:adt_opmp_comandos
	LOCAL l_jnl_codseq LIKE post:jnl_opmp_codseq_nro
	LOCAL l_jnl_pres LIKE post:PresNro
	LOCAL l_jnlret LIKE lib:err_retval
	LOCAL l_has_pending LIKE lib:err_retval

	LET l_has_pending = -2
	CLEARFIELD l_codseq
	CLEARFIELD l_pres
	CLEARFIELD l_jnlok
	CLEARFIELD l_codint
	CLEARFIELD l_cmd

	DPRINT "[DBG] RefRecov: ini fec=[%s] log=[%s] ter=[%s]\n" common:system_date op:op_login_id common:window_node
	DPRINT "[DBG] RefRecov: ini mp=[%s]\n" common:adt_mp_termi_integ

	CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
		WHERE post:adt_opmp_fecha == common:system_date \
		&& post:adt_opmp_login_id == op:op_login_id \
		&& post:adt_opmp_termi_win == common:window_node \
		&& post:adt_opmp_operacion == "DEV" \
		&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
		&& post:adt_opmp_codint_mp != "QRI" \
		&& post:adt_opmp_jnlOK == "N"

	CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
	LET l_retval = CDSRETVAL
	IF (l_retval == 0) THEN
		CDS EXTRACT DSC:post:OperacionesMP_ctx \
			post:adt_opmp_operacion \
			post:adt_opmp_comandos \
			post:adt_opmp_codint_mp \
			post:adt_opmp_jnlOK \
			post:adt_opmp_codseq_nro \
			post:adt_opmp_PresNro \
			post:adt_opmp_order_id \
			post:adt_opmp_order_id_PS \
			post:adt_opmp_ref_id \
			post:adt_opmp_ref_id_PS \
			post:adt_opmp_ref_st_PS \
			post:adt_opmp_st_det_PS \
			post:adt_opmp_status_p_PS \
			post:adt_opmp_payment_id

		LET l_has_pending = 0
		LET l_codseq = post:adt_opmp_codseq_nro
		LET l_pres = post:adt_opmp_PresNro
		LET l_jnlok = post:adt_opmp_jnlOK
		LET l_codint = post:adt_opmp_codint_mp
		LET l_cmd = post:adt_opmp_comandos
		DPRINT "[DBG] RefRecov: opmp enc=[%d] cod=[%s] pre=[%s] jnl=[%s] cin=[%s]\n" l_has_pending l_codseq l_pres l_jnlok l_codint
		DPRINT "[DBG] RefRecov: opmp cmd=[%s] ord=[%s] ref=[%s] st=[%s]\n" l_cmd post:adt_opmp_order_id_PS post:adt_opmp_ref_id_PS post:adt_opmp_ref_st_PS
	ELSE
		DPRINT "[DBG] RefRecov: opmp enc=[%d]\n" l_retval
	END IF
	CDS ENDSELECT DSC:post:OperacionesMP_ctx

	IF (l_has_pending != 0) THEN
		CLEARFIELD l_jnl_codseq
		CLEARFIELD l_jnl_pres
		LET l_jnlret = -2

		CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
			tlr:jnl_bus_date == common:system_date && \
			siaf:jnl_window_node == common:window_node && \
			tlr:jnl_op_login_id == op:op_login_id && \
			siaf:codigo_siaf == 99600 && \
			siaf:moneda == "06" && \
			post:jnl_opmp_codseq_nro > 0
		CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
		LET l_jnlret = CDSRETVAL
		IF (l_jnlret == 0) THEN
			CDS EXTRACT DSC:tlr:jnl_context post:jnl_opmp_codseq_nro post:PresNro INTO l_jnl_codseq l_jnl_pres
		END IF
		CDS ENDSELECT DSC:tlr:jnl_context

		IF (l_jnlret != 0) THEN
			CDS SELECT FROM DSC:tlr:TABjnl BECOMES DSC:tlr:jnl_context WHERE \
				tlr:jnl_bus_date == common:system_date && \
				siaf:jnl_window_node == common:window_node && \
				tlr:jnl_op_login_id == op:op_login_id && \
				siaf:codigo_siaf == 99601 && \
				siaf:moneda == "06" && \
				post:jnl_opmp_codseq_nro > 0
			CDS FINDFIRST DSC:tlr:jnl_context NOLOCK
			LET l_jnlret = CDSRETVAL
			IF (l_jnlret == 0) THEN
				CDS EXTRACT DSC:tlr:jnl_context post:jnl_opmp_codseq_nro post:PresNro INTO l_jnl_codseq l_jnl_pres
			END IF
			CDS ENDSELECT DSC:tlr:jnl_context
		END IF

		DPRINT "[DBG] RefRecov: jnl chk rv=[%d] cod=[%s] pre=[%s]\n" l_jnlret l_jnl_codseq l_jnl_pres

		IF (l_jnlret == 0) THEN
			CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
				WHERE post:adt_opmp_fecha == common:system_date \
				&& post:adt_opmp_login_id == op:op_login_id \
				&& post:adt_opmp_termi_win == common:window_node \
				&& post:adt_opmp_operacion == "DEV" \
				&& post:adt_opmp_comandos == "POINT_REFUND_POST" \
				&& post:adt_opmp_codint_mp != "QRI" \
				&& post:adt_opmp_codseq_nro == l_jnl_codseq

			CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
			LET l_retval = CDSRETVAL
			IF (l_retval == 0) THEN
				CDS EXTRACT DSC:post:OperacionesMP_ctx \
					post:adt_opmp_codseq_nro \
					post:adt_opmp_PresNro \
					post:adt_opmp_jnlOK \
					post:adt_opmp_codint_mp \
					post:adt_opmp_comandos
				LET l_codseq = post:adt_opmp_codseq_nro
				LET l_pres = post:adt_opmp_PresNro
				LET l_jnlok = post:adt_opmp_jnlOK
				LET l_codint = post:adt_opmp_codint_mp
				LET l_cmd = post:adt_opmp_comandos
				IF (l_jnlok != "S") THEN
					LET l_has_pending = 0
				END IF
				DPRINT "[DBG] RefRecov: jnl->opmp cod=[%s] pre=[%s] jnl=[%s] cmd=[%s]\n" l_codseq l_pres l_jnlok l_cmd
			END IF
			CDS ENDSELECT DSC:post:OperacionesMP_ctx
		END IF
	END IF

	IF (l_has_pending == 0) THEN
		MSGBOX "SE DETECTO UN REEMBOLSO SIN COMPLETARSE POR UNA CAIDA O INTERRUPCION. SE PROCEDE A COMPLETARLO ANTES DE INICIAR EL PROGRAMA",OFB_OK,"AVISO"
		DPRINT "[DBG] RefRecov: pre-dev cod=[%s] pre=[%s]\n" post:adt_opmp_codseq_nro post:adt_opmp_PresNro
		DPRINT "[DBG] RefRecov: pre-dev cin=[%s] jnl=[%s] cmd=[%s]\n" post:adt_opmp_codint_mp post:adt_opmp_jnlOK post:adt_opmp_comandos

		CALL post:MISCmpTipoCaida("DEV")
		LET l_rec_ret = RETVAL
		DPRINT "[DBG] RefRecov: ret dev=[%d]\n" l_rec_ret

		LET l_codseq = post:adt_opmp_codseq_nro
		LET l_pres = post:adt_opmp_PresNro
		CLEARFIELD l_jnlok
		CDS SELECT FROM DSC:post:OperacionesMP_tbl BECOMES DSC:post:OperacionesMP_ctx \
			WHERE post:adt_opmp_fecha == common:system_date \
			&& post:adt_opmp_login_id == op:op_login_id \
			&& post:adt_opmp_termi_win == common:window_node \
			&& post:adt_opmp_codseq_nro == l_codseq
		CDS FINDFIRST DSC:post:OperacionesMP_ctx NOLOCK
		IF (CDSRETVAL == 0) THEN
			CDS EXTRACT DSC:post:OperacionesMP_ctx post:adt_opmp_jnlOK INTO l_jnlok
		END IF
		CDS ENDSELECT DSC:post:OperacionesMP_ctx
		DPRINT "[DBG] RefRecov: fin db cod=[%s] pre=[%s] jnl=[%s]\n" l_codseq l_pres l_jnlok

		IF (l_rec_ret == 0 && l_jnlok == "S") THEN
			MSGBOX "SE COMPLETO EL REEMBOLSO INCONCLUSO",OFB_OK,"AVISO"
			RETURN 0
		END IF

		MSGBOX "NO SE PUDO COMPLETAR EL REEMBOLSO INCONCLUSO. CONTINUE CON EL LOGIN.",OFB_OK,"AVISO"
		RETURN -2
	END IF

	RETURN -2
END SUB

/*******/
