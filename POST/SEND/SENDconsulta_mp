/****o* Mercado Pago/OFB:post:SENDconsulta_mp
* NOMBRE
*   OFB:post:SENDconsulta_mp
* DESCRIPCION
*   Rutina que setea los bits de envío de mensajes de Web Services Mercado Pago
* ENTRADAS
*   Parámetros de entrada de la rutina, sean argumentos o campos que tiene en cuenta
* SALIDAS
*   Campos modificados como resultado o returns cuyos valores sean no triviales
* IMPACTOS
*   Otros objetos que deberán ser analizados al modificar el objeto actual
* REFERENCIADO POR
*   OFB:post:OFBconsume_ws_mp
* DOCUMENTACION
*   NI50546 - Mercado Pago
* SOURCE
*/
#include "C:\moaproj\V47.10\src\POST\SEND\postSEND.h"

SUB post:SENDconsulta_mp
	LOCAL fechahora LIKE common:csr_string
	LOCAL auxiliar LIKE common:csr_string
	LOCAL l_Usuario LIKE op:NTUserName
	// Variables auxiliares para Smart Point
	LOCAL type LIKE common:csr_string
	LOCAL ticket_number LIKE common:csr_string
	LOCAL external_reference LIKE common:csr_string
	LOCAL print_on_terminal LIKE common:csr_string
	LOCAL terminal_id LIKE common:csr_string
	
	CALL post:OFBclear_fldsMsg_mp

	CALL post:MISCseteobit_mp(1,"")
	
	// Se verifica si es Smart Point
	IF (post:adt_tmp_codint_mp == "MPC" || post:adt_tmp_codint_mp == "MPD") THEN
		// Se setea el bin de servicio para derivarse el request hacia el servicio de Smart Point
		CALL post:MISCseteobit_mp(2,0x13)
	ELSE
		// Se setea el bin de servicio para derivarse el request hacia el servicio de QR
		CALL post:MISCseteobit_mp(2,0x12)
	END IF

	CALL post:MISCseteobit_mp(3,giros:codigo_procesamiento)

	FPRINT fechahora,"%02.2s%02.2s%6.6s",\
	SUBSTR(giros:dia_viaje,4,2),SUBSTR(giros:dia_viaje,6,2),giros:hora_viaje
	CALL post:MISCseteobit_mp(7,fechahora)

	FPRINT auxiliar,"%06.6s",substr(post:adt_codseq_nro,0,6)
	CALL post:MISCseteobit_mp(11,auxiliar)
	
	FPRINT auxiliar,"%6.6s",giros:hora_viaje
	CALL post:MISCseteobit_mp(12,auxiliar)
	
	FPRINT auxiliar,"%02.2s%02.2s",\
		SUBSTR(giros:dia_viaje,4,2),SUBSTR(giros:dia_viaje,6,2)
	CALL post:MISCseteobit_mp(13,auxiliar)

	CALL post:MISCseteobit_mp(24,"")

	CALL post:MISCseteobit_mp(41,"")

	CALL post:MISCseteobit_mp(60,"")
	
	CALL op:MISCobtengo_UserName
    l_Usuario = op:NTUserName
	//bit 108 - NIS	Char(5)-ID de aplicación	Char(2) - Usuario	Char(14)	
	FPRINT post:BulkFld_108, "%5.5s%2.2s%-14.14s", common:sucursal_activa,"2",l_Usuario
	CALL post:MISCseteobit_mp(108,post:BulkFld_108)

	CLEARFIELD post:mp_ws_parametros

	SELECT (giros:codigo_procesamiento)
		CASE 100010
			//POINT Integrado - Consulta Orden de Pago
			FPRINT post:mp_ws_parametros "%-32s" post:mp_order_id_PS
		BREAK
		CASE 100011
			//POINT Integrado - Crea Orden de Pago  

			/* ***************************** */
			/* *** VARIABLES INTERMEDIAS *** */
			/* ***************************** */

			// Se arma el External Reference en funcion de las variables que lo componen
			/* 1 - (2) Inicio del External Reference */
			/* 2 - (5) NIS + (1) W + (3) ultimos 3 digitos de Terminal */
			/* 3 - (5) Operator ID */
			/* 4 - (4) Anio */
			/* 5 - (2) Mes */
			/* 6 - (2) Dia */
			/* 7 - (6) Hora de la transaccion */
			/* 8 - (8) Secuenciador relleno con 0s a izquierda */
			//                         " 1    2    3    4    5    6    7    8    "
			FPRINT external_reference, "%2.2s%9.9s%5.5s%4.4s%2.2s%2.2s%6.6s%08.8s", \
			"PS", \
			SUBSTR(post:mp_ext_reference,2,9), \
			op:op_operator_id, \
			SUBSTR(giros:dia_viaje,0,4), \
			SUBSTR(giros:dia_viaje,4,2), \
			SUBSTR(giros:dia_viaje,6,2), \
			giros:hora_viaje, \
			post:adt_mp_codseq_nro

			// Se arma el Ticket Number en funcion de las variables que lo componen
			/* 1 - (2) Inicio del Ticket Number */
			/* 2 - (5) NIS + (1) W + (3) ultimos 3 digitos de Terminal */
			/* 3 - (8) Secuenciador relleno con 0s a izquierda */
			//                    " 1    2    3    "
			FPRINT ticket_number, "%2.2s%9.9s%08.8s", \
			"PS", \
			SUBSTR(post:mp_ext_reference, 2, 9), \
			post:adt_mp_codseq_nro

			// Se retiran los posibles espacios en blanco en las variables
			CALL CSR:trimfield(post:adt_tmp_term_type)
			CALL CSR:trimfield(post:adt_tmp_dev_serial)

			// Se arma el Terminal ID en funcion de las variables que lo componen
			/* 1 - (30) Tipo de terminal */
			/* 2 - (2) Separador */
			/* 3 - (50) Serial de la terminal */
			// Se genera la concatenacion de los datos en una variable auxiliar
			//               " 1 2  3"
			FPRINT auxiliar, "%s%s%-s", \
				post:adt_tmp_term_type, \
				"__", \
				post:adt_tmp_dev_serial

			// Se pasa la variable auxiliar a "terminal_id", alineando a derecha
			FPRINT terminal_id, "%82s", auxiliar

			// Se define el tipo de Smart Point que se usara
			/* 1 - Por ahora, va a ser siempre que no imprima */
			//                        " 1   "
			FPRINT print_on_terminal, "%2.2s", \
			"NT"

			// Se define el tipo de Smart Point que se usara
			/* 1 - Por ahora, va a ser siempre Point */
			//           " 1   "
			FPRINT type, "%5.5s", \
			"Point"

			/* ***************************************** */
			/* *** MENSAJE DE SALIDA EN EL CAMPO 109 *** */
			/* ***************************************** */

			// Se arma el campo 109 segun el orden establecido en el documento del detallado de mensajeria para Point
			/* 1 - (5)	Tipo de Smart Point utilizado */
			/* 2 - (38)	External Reference, usado internamente para identificar la operacion */
			/* 3 * (12)	Monto relleno con 0s a izquierda */
			/* 4 - (20)	Ticket Number de la operacion */
			/* 5 - (2)	Print On Terminal */
			/* 6 - (12)	Metodo de pago */
			/* 7 -  (2)	Cuotas elegidas */
			/* 8 - (82)	ID de la terminal */
			//							  " 1    2      3       4      5    6      7     8     "
			FPRINT post:mp_ws_parametros, "%5.5s%38.38s%012.12s%20.20s%2.2s%12.12s%02.2s%82.82s", \
			type, \
			external_reference, \
			post:mp_amount, \
			ticket_number, \
			print_on_terminal, \
			post:mp_cc_type, \
			post:Nro_Cuotas_Pos, \
			terminal_id

		BREAK
		CASE 100020
			//POINT Integrado - Elimina Orden de Pago 
			FPRINT post:mp_ws_parametros "%-32s" post:mp_order_id_PS
		BREAK
;		CASE 110010
;			//QR Integrado - Consulta Orden de Pago
;			//NO EXISTE Consulta de Orden de Pago para QR Integrado
;		BREAK
		CASE 110011
			//QR Integrado - Crea Orden de Pago  	
			FPRINT post:mp_ws_parametros "%-12.12s%-20.20s%-9.9s%-15.15s%-1.1s" \
				post:mp_amount post:mp_description post:mp_device_name post:mp_ext_reference post:mp_quantity
		BREAK
		CASE 100015
			//POINT Integrado - Consulta Pago Extra Data
			//FPRINT post:mp_ws_parametros "%10.10s" post:mp_payment_id
			FPRINT post:mp_ws_parametros "%20.20s" post:mp_payment_id  
		BREAK
		CASE 110020
			//QR Integrado - Elimina Orden de Pago 
			//pos_id, identificador único del punto de venta
			FPRINT post:mp_ws_parametros "%10.10s" post:mp_device_name
		BREAK		
		CASE 200010
			//POINT Integrado - Consulta un Pago   
			//FPRINT post:mp_ws_parametros "%10.10s" post:mp_payment_id 
			FPRINT post:mp_ws_parametros "%20.20s" post:mp_payment_id
		BREAK
		CASE 200015
			//POINT Integrado - Consulta un Pago (Extra info Point)
			//FPRINT post:mp_ws_parametros "%10.10s" post:mp_payment_id
			FPRINT post:mp_ws_parametros "%20.20s" post:mp_payment_id 
		BREAK
		CASE 210010
			//POINT Integrado - Consulta un Pago   
			//FPRINT post:mp_ws_parametros "%10.10s" post:mp_payment_id
			FPRINT post:mp_ws_parametros "%20.20s" post:mp_payment_id  
		BREAK
		CASE 210015
			//QR Integrado - Busca un Pago   
			FPRINT post:mp_ws_parametros "%15.15s%20.20s" post:mp_ext_reference post:mp_description
		BREAK		
		CASE 200020
			//QR Integrado - Devolución de un Pago    
			//FPRINT post:mp_ws_parametros "%10.10s" post:mp_payment_id 
			FPRINT post:mp_ws_parametros "%20.20s" post:mp_payment_id 
		BREAK
		CASE 200025
			//POINT Integrado - Reembolso de un Pago
			FPRINT post:mp_ws_parametros "%-32s" post:mp_order_id_PS
		BREAK
		CASE 300010
			//POINT Integrado - Consulta Device
			FPRINT post:mp_ws_parametros "%9.9s" post:mp_device_name 
		BREAK
		CASE 300020
			//POINT Integrado - Elimina Device
			FPRINT post:mp_ws_parametros "%9.9s" post:mp_device_name 
		BREAK
	END SELECT

	DPRINT "post:mp_ws_parametros [%s]\n" post:mp_ws_parametros

	//bit 109 - Parametros Web Services Mercado Pago
	MOVE post:mp_ws_parametros TO post:BulkFld_109
	CALL post:MISCseteobit_mp(109,post:BulkFld_109)
	
	//bit 117 
	CALL post:MISCarma_firma_mp
	CALL giros:MISCfirma
	giros:firma_enviada = common:csr_string_171
	CALL post:MISCseteobit_mp(117,common:csr_string_171)

	CALL post:SENDhdr_mp

END SUB

/*******/
